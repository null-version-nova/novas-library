import com.replaymod.gradle.preprocess.PreprocessTask

if (project.name != "root") {
    project.ext.minecraft_version = project.name
} else {
    project.ext.minecraft_version = primary_minecraft_version
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.modrinth.minotaur"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.18.2+build.4:v2"
        annotationProcessor 'systems.manifold:manifold-preprocessor:2024.1.1'
        testAnnotationProcessor 'systems.manifold:manifold-preprocessor:2024.1.1'
    }
}

allprojects {
    def mcVersion
    def (major, minor, patch) = project.minecraft_version.tokenize('-')[0].tokenize('.')
    mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}" as int
    project.ext.mcVersion=mcVersion

    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"



    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

apply plugin: 'com.replaymod.preprocess'

def architectury_version = [
        11802: "4.11.93",
        12001: "9.1.12"
][project.mcVersion]
project.ext.architectury_version = architectury_version

def forge_version = [
        11802: "40.2.10",
        12001: "47.2.19"
][project.mcVersion]
project.ext.forge_version = forge_version

def kotlin_for_forge_version = [
        11802: "3.12.0",
        12001: "4.10.0"
][project.mcVersion]
project.ext.kotlin_for_forge_version = kotlin_for_forge_version

def fabric_api_version = [
        11802: "0.76.0",
        12001: "0.91.0"
][project.mcVersion]
project.ext.fabric_api_version = fabric_api_version